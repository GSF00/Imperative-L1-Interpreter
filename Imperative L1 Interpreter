(*++++++++++++++++++++++++++++++++++++++*)
(*  Interpretador para L1 imperativa    *)
(*  - inferência de tipos monomórfica   *)
(*  - avaliador big step com ambiente   *)
(*++++++++++++++++++++++++++++++++++++++*)


(*++++++++++++++++++++++++++++++++++++++++++*)
(*  SINTAXE, AMBIENTE de TIPOS e de VALORES *)
(*++++++++++++++++++++++++++++++++++++++++++*)

(* tipos da linguagem L1 *)

type tipo =
    TyInt
  | TyBool
  | TyFn of tipo * tipo
  | TyPair of tipo * tipo
              
    (*extensão imperativa*)

  | TyRef of tipo
  | TyUnit
  
(* expressões da linguagem L1 *)

type ident = string
  
type endereco = int

type op = Sum | Sub | Mult | Eq | Gt | Lt | Geq | Leq

type expr =
  | Num of int
  | Var of ident
  | Bcte of bool
  | Binop of op * expr * expr
  | Pair of expr * expr
  | Fst of expr
  | Snd of expr
  | If of expr * expr * expr
  | Fn of ident * tipo * expr
  | App of expr * expr
  | Let of ident * tipo * expr * expr
  | LetRec of ident * tipo * expr  * expr
              
    (*extensão imperativa*)

  | Asg of expr * expr
  | Dref of expr
  | New of expr
  | Seq of expr * expr
  | Whl of expr * expr
  | Skip
  | L of endereco
            

 (* ambiente de tipo, valores, ambiente de execução e memória *)

type tenv = (ident * tipo) list
    
type valor =
    VNum of int
  | VBool of bool
  | VPair of valor * valor
  | VClos  of ident * expr * renv
  | VRclos of ident * ident * expr * renv
  | VSkip
  | VL of endereco
and 
  renv = (ident * valor) list
       (*| memoria = (endereco * valor) list*)
  
type memoria = (endereco * valor) list
    

    (* funções de busca e de atualização de ambientes *)   

let rec lookup a k  =
  match a with
    [] -> None
  | (y,i) :: tl -> if (y=k) then Some i else lookup tl k
       
let  update a k i = (k,i) :: a 
                    
  
    (* função length para memoria *)

let rec length (mem:memoria) : int =
  (match mem with
     [] -> 0
   | _ :: tl -> 1 + length tl)
  
  
  
(**+++++++++++++++++++++++++++++++++++++++++*)
(*         INFERÊNCIA DE TIPOS              *)
(*++++++++++++++++++++++++++++++++++++++++++*)

(* exceções que não devem ocorrer *) 

exception BugParser
  
exception BugTypeInfer 
                       
(* exceções para usuário da linguagem L1 *)

exception TypeError of string
    
exception AddressError of string


let rec typeinfer (tenv:tenv) (e:expr) : tipo =
  match e with

    (* TInt  *)
  | Num _ -> TyInt

    (* TVar *)
  | Var x ->
      (match lookup tenv x with
         Some t -> t
       | None -> raise (TypeError ("variavel nao declarada:" ^ x)))

    (* TBool *)
  | Bcte _ -> TyBool 

    (* Top+ e outros operadores binários *)
  | Binop(oper,e1,e2) ->  
      let t1 = typeinfer tenv e1 in
      let t2 = typeinfer tenv e2 in 
      (match (t1,t2) with
         (TyInt, TyInt) -> 
           (match oper with 
              Sum | Sub | Mult         ->  TyInt
            | Eq | Gt | Lt | Geq | Leq ->  TyBool)
       | _ -> raise (TypeError "pelo menos um dos operandos de operador binário não é do tipo int"))

  (* TPair *)
  | Pair(e1,e2) -> 
      let t1 = typeinfer tenv e1 in 
      let t2 = typeinfer tenv e2 in
      TyPair(t1,t2)
 
  (* TFst *)
  | Fst e1 ->
      let t1 = typeinfer tenv e1 in 
      (match t1 with
         TyPair(t,_) -> t
       | _           -> raise (TypeError "fst espera tipo par ordenado"))
  
  (* TSnd *)
  | Snd e1 ->
      let t1 = typeinfer tenv e1 in
      (match t1 with
         TyPair(_,t) -> t
       | _           -> raise (TypeError "snd espera tipo par ordenado"))

    (* TIf *)
  | If(e1,e2,e3) ->
      let t1 = typeinfer tenv e1 in 
      (match t1 with
         TyBool ->
           let t2 = typeinfer tenv e2 in
           let t3 = typeinfer tenv e3
           in if t2 = t3 then t2
           else raise (TypeError "then/else com tipos diferentes")
       | _     -> raise (TypeError "condição de IF não é do tipo bool"))

    (* TFn *)
  | Fn(x,t,e1) ->
      let t1 = typeinfer (update tenv x t) e1
      in TyFn(t,t1)

    (* TApp *)
  | App(e1,e2) ->
      let t1 = typeinfer tenv e1 in
      let t2 = typeinfer tenv e2 in 
      (match t1 with
         TyFn(t_in, t_out) ->  
           if t2 = t_in then t_out
           else raise (TypeError "tipo argumento errado" )
       | _ -> raise (TypeError "tipo função era esperado"))

    (* TLet *)
  | Let(x,t,e1,e2) ->
      if (typeinfer tenv e1) = t then typeinfer (update tenv x t) e2
      else raise (TypeError "expr nao é do tipo declarado em Let" )

    (* TLetRec *)
  | LetRec(f,(TyFn(t1,t2) as tf), Fn(x,tx,e1), e2) ->
      let tenv_com_tf = update tenv f tf in
      let tenv_com_tf_tx = update tenv_com_tf x tx in
      if (typeinfer tenv_com_tf_tx e1) = t2
      then typeinfer tenv_com_tf e2
      else raise (TypeError "tipo da funcao diferente do declarado")
          
  | LetRec _ -> raise BugParser 
  
  (*extensão imperativa*)
  
    (*TAtr*)
  | Asg(e1,e2) ->
      let t1 = typeinfer tenv e1 in
      let t2 = typeinfer tenv e2 in
      if t1 == TyRef(t2) then TyUnit
      else raise (TypeError "tipos incompatíveis na atribuição") 
   
    (*TDeref*)
  | Dref(e1) ->
      let t1 = typeinfer tenv e1 in
      (match t1 with
         TyRef(t) -> t
       | _ -> raise (TypeError "tipo imcompatível na desreferenciação"))
      
    (*TNew*) 
  | New(e1) ->
      let t1 = typeinfer tenv e1 in
      TyRef(t1)
        
    (*TSkip*)
  | Skip -> TyUnit
    
    (*TWhile*)
  | Whl(e1,e2) ->
      let t1 = typeinfer tenv e1 in
      let t2 = typeinfer tenv e2 in
      (match t1 with
         TyBool ->
           (match t2 with
              TyUnit -> TyUnit
            | _      -> raise (TypeError "segundo operando de Whl não é do tipo TyUnit"))
       | _      -> raise (TypeError "primeiro operando de Whl não é do tipo TyBool")) 
    
    (*TSeq*)
  | Seq(e1,e2) ->
      let t1 = typeinfer tenv e1 in
      let t2 = typeinfer tenv e2 in
      (match t1 with
         TyUnit -> t2
       | _ -> raise (TypeError "primeiro operando de Seq não é do tipo TyUnit"))
      
  | L _ -> raise BugParser
      
      
           
        
      
  
(**+++++++++++++++++++++++++++++++++++++++++*)
(*                 AVALIADOR                *)
(*++++++++++++++++++++++++++++++++++++++++++*)




let rec compute (oper: op) (v1: valor) (v2: valor) : valor =
  match (oper, v1, v2) with
  
    (* operadores aritméticos com números *)
    (Sum,  VNum n1, VNum n2) -> VNum (n1 + n2)
  | (Sub,  VNum n1, VNum n2) -> VNum (n1 - n2)
  | (Mult, VNum n1, VNum n2) -> VNum (n1 * n2) 
                                                
    (* operadores relacionais com números *)
  | (Eq, VNum n1, VNum n2) -> VBool( n1 = n2) 
  | (Gt, VNum n1, VNum n2) -> VBool( n1 > n2)
  | (Lt, VNum n1, VNum n2) -> VBool( n1 < n2)
  | (Geq,VNum n1, VNum n2) -> VBool( n1 >= n2)
  | (Leq,VNum n1, VNum n2) -> VBool( n1 <= n2) 
                                
  | _ -> raise BugTypeInfer


let rec eval (renv:renv) (e:expr) (mem:memoria) : valor =
  match e with
    Num n -> VNum n
               
  | Bcte b -> VBool b

  | Var x ->
      (match lookup renv x with
         Some v -> v
       | None -> raise BugTypeInfer)
      
  | Binop(oper,e1,e2) ->
      let v1 = eval renv e1 mem in
      let v2 = eval renv e2 mem in
      compute oper v1 v2

  | Pair(e1,e2) ->
      let v1 = eval renv e1 mem in
      let v2 = eval renv e2 mem in
      VPair(v1,v2)

  | Fst e ->
      (match eval renv e mem with
       | VPair(v1,_) -> v1
       | _ -> raise BugTypeInfer)

  | Snd e ->
      (match eval renv e mem with
       | VPair(_,v2) -> v2
       | _ -> raise BugTypeInfer)

  | If(e1,e2,e3) ->
      (match eval renv e1 mem with
         VBool true  -> eval renv e2 mem
       | VBool false -> eval renv e3 mem
       | _ -> raise BugTypeInfer)

  | Fn (x,_,e1) ->  VClos(x,e1,renv)

  | App(e1,e2) ->
      let v1 = eval renv e1 mem in
      let v2 = eval renv e2 mem in
      (match v1 with
         VClos(x,ebdy,renv') ->
           let renv'' = update renv' x v2
           in eval renv'' ebdy mem

       | VRclos(f,x,ebdy,renv') ->
           let renv''  = update renv' x v2 in
           let renv''' = update renv'' f v1
           in eval renv''' ebdy mem
       | _ -> raise BugTypeInfer)

  | Let(x,_,e1,e2) ->
      let v1 = eval renv e1 mem
      in eval (update renv x v1) e2 mem

  | LetRec(f,TyFn(t1,t2),Fn(x,tx,e1), e2) when t1 = tx ->
      let renv'= update renv f (VRclos(f,x,e1,renv))
      in eval renv' e2 mem
        
  | LetRec _ -> raise BugParser 
                  
    (*extensão imperativa*)

  | Skip -> VSkip
    
  | L n -> VL n 

  | Asg(e1,e2) ->
      let l = eval renv e1 mem in
      let v = eval renv e2 mem in
      (match l with
         VL n ->
           let mem' = update mem n v in
           eval renv Skip mem'
       | _ -> raise BugTypeInfer)
      
(**)
  | Dref(e1) ->
      let l = eval renv e1 mem in
      (match l with
         VL n ->
           (match lookup mem n with
              Some v -> v
            | None -> raise (AddressError "erro no avaliador."))
       | _ -> raise (AddressError "argumento errado para Dref."))
      
  | New(e1) ->
      let v1 = eval renv e1 mem in
      let index = length mem in
      let mem' = update mem index v1 in
      eval renv (L index) mem'
      
        
  | Whl(e1, e2) ->
      eval renv (If(e1, Seq(e2, Whl(e1, e2)), Skip)) mem
        
  | Seq(e1,e2) ->
      let v1 = eval renv e1 mem in
      let v2 = eval renv e2 mem in
      (match v1 with
         VSkip -> v2
       | _ -> raise BugTypeInfer)
  
        (*| _ -> raise (TypeError "Não implementado")*)
    
(* função auxiliar que converte tipo para string *)

let rec ttos (t:tipo) : string =
  match t with
    TyInt  -> "int"
  | TyBool -> "bool"
  | TyFn(t1,t2)   ->  "("  ^ (ttos t1) ^ " --> " ^ (ttos t2) ^ ")"
  | TyPair(t1,t2) ->  "("  ^ (ttos t1) ^ " * "   ^ (ttos t2) ^ ")"
  | TyRef (t1)    ->  "(" ^ (ttos t1) ^ " ref" ^ ")"
  | TyUnit -> "unit"
    
(* função auxiliar que converte valor para string *)

let rec vtos (v: valor) : string =
  match v with
    VNum n -> string_of_int n
  | VBool b -> string_of_bool b 
  | VPair(v1, v2) ->
      "(" ^ vtos v1 ^ "," ^ vtos v1 ^ ")"
  | VClos _ ->  "fn"
  | VRclos _ -> "fn"
  | VSkip -> "skip"
  | VL n -> "*" ^ string_of_int n

(* principal do interpretador *)

let int_bse (e:expr) (mem:memoria) : unit =
  try
    let t = typeinfer [] e in
    let v = eval [] e mem in
    print_string ((vtos v) ^ " : " ^ (ttos t))
  with
    TypeError msg ->  print_string ("erro de tipo - " ^ msg)
   
 (* as exceções abaixo nao devem ocorrer   *)
  | BugTypeInfer  ->  print_string "corrigir bug em typeinfer"
  | BugParser     ->  print_string "corrigir bug no parser para let rec"
                        



 (* +++++++++++++++++++++++++++++++++++++++*)
 (*                TESTES                  *)
 (*++++++++++++++++++++++++++++++++++++++++*)



(*
     let x:int = 2 
     in let foo: int --> int = fn y:int => x + y 
        in let x: int = 5
           in foo 10 
           
    do tipo int , avalia para 12 
*)

let e'' = Let("x", TyInt, Num 5, App(Var "foo", Num 10))

let e'  = Let("foo", TyFn(TyInt,TyInt), Fn("y", TyInt, Binop(Sum, Var "x", Var "y")), e'')

let tst = Let("x", TyInt, Num(2), e') 
    
    (*
     let x:int = 2 
     in let foo: int --> int = fn y:int => x + y 
        in let x: int = 5
           in foo 
           
      do tipo int --> int avalia para uma função 
*)


let e2 = Let("x", TyInt, Num 5, Var "foo")

let e1  = Let("foo", TyFn(TyInt,TyInt), Fn("y", TyInt, Binop(Sum, Var "x", Var "y")), e2)

let tst2 = Let("x", TyInt, Num(2), e1) 


  
    (* let x:int = 10
       in let f: int --> int = fn y:int => y + x
          in let x:int  = 25
             in f 100
           
     do tipo int avalia para 110 
*)

let e3 = Let("x", TyInt, Num 25, App(Var "f", Num 100))
            
let e2 = Let("f", TyFn(TyInt,TyInt), Fn("y", TyInt, Binop(Sum, Var "y", Var "x")), e3)
            
let tst3 = Let("x", TyInt, Num 10, e2) 
    
  


  (*   com acúcar sintático:

let rec pow (x:int) (y:int) : int = 
  if y = 0 then 1 else x * (pow x (y-1))  
in (pow 3) 4 
     
  sem açucar sintático:

     let rec pow: int -> (int --> int) = 
       fn x:int => fn y:int => if y = 0 then 1 else x * (pow x (y-1)) 
     in (pow 3) 4 
     
do tipo int avalia para  81

*)          

let ymenos1 = Binop(Sub, Var "y", Num 1)     (* y - 1  *)
  
let powapp  =   App(App(Var "pow", Var "x"), ymenos1)   (* (pow x)  (y-1) *)
                                           
let xpow =   Binop(Mult, Var "x", powapp)    (*   x * (pow x (y-1))   *)
      
    (* fn y:int => if y=0 then 1 else x * (pow x (y-1))    *) 
let ebdy = Fn("y",
              TyInt,
              If(Binop(Eq, Var "y", Num 0) , Num 1, xpow))  
  
let pow = 
  LetRec("pow", 
         TyFn(TyInt, TyFn(TyInt,TyInt)), (* pow: int --> int --> int*)
         Fn("x", TyInt, ebdy),           (* fn x: int => ebdy  *)
         App(App(Var "pow", Num 3), Num 4)) (* in  (pow 3) 4    *)
                                            
                                            
  
    (*  5 |> inc 
    
        do tipo int avalia para 6   *)


let dobro = Fn("y", TyInt, Binop(Mult, Var "y", Num 2))
    
let inc = Fn("y", TyInt, Binop(Sum, Var "y", Num 1))
